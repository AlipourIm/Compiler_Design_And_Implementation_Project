Program -> Declaration_list $
Declaration_list -> Declaration Declaration_list
Declaration_list -> ε
Declaration -> Declaration_initial Declaration_prime
Declaration_initial -> #push_type Type_specifier #decl_id ID
Declaration_prime -> Fun_declaration_prime
Declaration_prime -> Var_declaration_prime
Var_declaration_prime -> #end_decl_var ;
Var_declaration_prime -> [ #push_num NUM ] #end_decl_arr ;
Fun_declaration_prime -> ( Params ) Compound_stmt
Type_specifier -> int
Type_specifier -> void
Params -> int ID Param_prime Param_list
Params -> void
Param_list -> , Param Param_list
Param_list -> ε
Param -> Declaration_initial Param_prime
Param_prime -> [ ]
Param_prime -> ε
Compound_stmt -> { #inc_scope Declaration_list Statement_list } #dec_scope
Statement_list -> Statement Statement_list
Statement_list -> ε
Statement -> Expression_stmt
Statement -> Compound_stmt
Statement -> Selection_stmt
Statement -> Iteration_stmt
Statement -> Return_stmt
Expression_stmt -> Expression ;
Expression_stmt -> break ;
Expression_stmt -> ;
Selection_stmt -> if ( Expression ) Statement Else_stmt
Else_stmt -> endif
Else_stmt -> else Statement endif
Iteration_stmt -> repeat Statement until ( Expression )
Return_stmt -> return Return_stmt_prime
Return_stmt_prime -> ;
Return_stmt_prime -> Expression ;
Expression -> Simple_expression_zegond
Expression -> #push_id ID B
B -> = Expression #direct_assign
B -> [ Expression ] H
B -> Simple_expression_prime
H -> = Expression
H -> G D C
Simple_expression_zegond -> Additive_expression_zegond C
Simple_expression_prime -> Additive_expression_prime C
C -> #push_op Relop Additive_expression #operate
C -> ε
Relop -> <
Relop -> ==
Additive_expression -> Term D
Additive_expression_prime -> Term_prime D
Additive_expression_zegond -> Term_zegond D
D -> #push_op Addop Term #operate D
D -> ε
Addop -> +
Addop -> -
Term -> Factor G
Term_prime -> Factor_prime G
Term_zegond -> Factor_zegond G
G -> #push_op * Factor #operate G
G -> ε
Factor -> ( Expression )
Factor -> #push_id ID Var_call_prime
Factor -> #push_num NUM
Var_call_prime -> ( Args )
Var_call_prime -> Var_prime
Var_prime -> [ Expression ]
Var_prime -> ε
Factor_prime -> ( Args )
Factor_prime -> ε
Factor_zegond -> ( Expression )
Factor_zegond -> #push_num NUM
Args -> Arg_list
Args -> ε
Arg_list -> Expression Arg_list_prime
Arg_list_prime -> , Expression Arg_list_prime
Arg_list_prime -> ε